apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'signing'

mainClassName = 'net.skyscanner.hello.HelloWorld'
group = "net.skyscanner.hello"
archivesBaseName = "hello-ossrh"
version = "0.0.1"

// tag::repositories[]
repositories { 
    mavenCentral() 
}
// end::repositories[]

// tag::jar[]
jar {
    archiveBaseName = 'gs-gradle'
    archiveVersion =  '0.1.0'
}
// end::jar[]

// tag::dependencies[]
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation "joda-time:joda-time:2.2"
    testImplementation "junit:junit:4.12"
}
// end::dependencies[]

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

signing {
    sign configurations.archives
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Skyscanner OSSRH sample app'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'A application used to claim net.skyscanner namespace on OSSRH'
        url 'http://github.com/Skyscanner/hello-ossrh'

        scm {
          connection 'scm:git:git://github.com/Skyscanner/hello-ossrh.git'
          developerConnection 'scm:git:ssh://github.com/Skyscanner/hello-ossrh.git'
          url 'http://github.com/Skyscanner/hello-ossrh/tree/main'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'skyscanner'
            name 'Skyscanner Open Source'
            email 'catalystsquad@skyscanner.net'
          }
        }
      }
    }
  }
}

// tag::wrapper[]
// end::wrapper[]
